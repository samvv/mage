
# Should be part of standard library

any_byte = [\x00-\xFF]

any_char = [\x00-\x7F]

# Special rules

@skip
whitespace = [\n\r\t ]*

# Tokens

## A name in the language.
##
## {accept}
## foobar
## {/accept}
##
## {accept}
## FooBar
## {/accept}
##
## {accept}
## F2Bar
## {/accept}
@keyword
pub token name
  = [a-z_]i [a-z0-9_]i*

## An integral number.
##
## {accept}
## 1
## {/accept}
##
## {accept}
## 5
## {/accept}
##
## {accept}
## 45
## {/accept}
##
## {accept}
## 0
## {/accept}
pub token integer -> Integer
  = [0-9]+

@noskip
hex_digit
  = [0-9a-f]i

@noskip
escape_sequence
  = 'u' '{' hex_digit{4} '}'
  | 'x' hex_digit{2}
  | escaped_ascii_char

@noskip
escaped_ascii_char
  = 'a'
  | 'b'
  | 'f'
  | 'n'
  | 'r'
  | 't'
  | 'v'
  | '\\'
  | '\''
  | '"'

## A single character in a character set expression.
##
## {accept}
## $
## {/accept}
##
## {accept}
## a
## {/accept}
##
## {accept}
## z
## {/accept}
##
## {accept}
## _
## {/accept}
##
## {accept}
## 0
## {/accept}
##
## {accept}
## 9
## {/accept}
##
## {accept}
## \x0A
## {/accept}
@noskip
single_char
  = '\\' escape_sequence
  | any_char

## A string literal.
##
## {accept}
## 'foo'
## {/accept}
##
## {accept}
## 'Hello, world!'
## {/accept}
##
## {accept}
## '$'
## {/accept}
##
## {accept}
## '++'
## {/accept}
pub token string
  = '\'' (!'\'' single_char)* '\''

# Values

pub value
  = integer
  | name

# Expressions

pub expr
  = char_set_expr
  | choice_expr
  | list_expr
  | lit_expr
  | label_expr
  | lookahead_expr
  | many_expr
  | negative_lookahead_expr
  | opt_expr
  | ref_expr
  | repeat_expr
  | seq_expr
  | some_expr

pub label_expr
  = name ':' expr

pub lit_expr
  = string

pub ref_expr
  = name

pub token char_set_range
  = single_char '-' single_char

pub token char_set_char
  = single_char

pub char_set_element
  = char_set_range | char_set_char

pub char_set_expr
  = '~'? '[' elements:(!']' char_set_element)* ']' 'i'?

pub seq_expr
  = elements:expr{2,}

pub choice_expr
  = elements:(expr %%% '|')

pub repeat_expr
  = expr '{' min:integer max:(',' integer?)? '}'

pub some_expr
  = expr '+'

pub many_expr
  = expr '*'

pub opt_expr
  = expr '?'

pub lookahead_expr
  = '&' expr

pub negative_lookahead_expr
  = '!' expr

pub list_expr
  = element:expr '%'+ separator:expr

# Rules

pub decorator
  = '@' name '{' args:(value % ',')  '}'

pub rule
  = decorator* 'pub'? 'extern'? 'token'? name:ident '=' expr

pub grammar
  = rule*
